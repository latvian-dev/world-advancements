import java.time.Instant

plugins {
	id 'base'
	id 'java'
	id 'maven-publish'
	id 'net.neoforged.moddev' version "1.0.14"
	// https://maven.architectury.dev/me/shedaniel/unified-publishing/maven-metadata.xml
	id "me.shedaniel.unified-publishing" version "0.1.13"
}

ext.ENV = System.getenv()
def isLocal = !ENV.containsKey("GITHUB_RUN_NUMBER")

version = "${mod_version}-${isLocal ? "local.${Instant.now().epochSecond}" : "build.${ENV.GITHUB_RUN_NUMBER}"}"
group = project.maven_group
base.archivesBaseName = project.archives_base_name

neoForge {
	version = project.neoforge_version

	runs {
		configureEach {
			logLevel = org.slf4j.event.Level.INFO
		}

		client {
			client()

			if (ENV.MC_CLIENT_ARGS) {
				programArguments.addAll(ENV.MC_CLIENT_ARGS.split(' '))
			}

			jvmArguments.addAll("-XX:+IgnoreUnrecognizedVMOptions", "-XX:+AllowEnhancedClassRedefinition")
			gameDirectory = file 'run'
		}

		server {
			server()
			programArgument("--nogui")
			gameDirectory = file 'run_server'
		}
	}

	mods {
		worldadvancements {
			sourceSet sourceSets.main
		}
	}

	parchment {
		minecraftVersion = parchment_mc_version
		mappingsVersion = parchment_mapping_version
	}
}

compileJava {
	options.encoding = "UTF-8"
	options.release.set(21)
}

java {
	sourceCompatibility = targetCompatibility = '21'
	withSourcesJar()
}

repositories {
	mavenLocal()
	mavenCentral()

	maven {
		url "https://maven.neoforged.net/releases"
	}

	maven {
		name = 'ParchmentMC'
		url = 'https://maven.parchmentmc.org'
		content {
			includeGroup "org.parchmentmc.data"
		}
	}

	maven {
		url "https://cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
}

dependencies {
	// compileOnly("curse.maven:oculus-581495:5299671")
	// compileOnly("curse.maven:rubidium-574856:4952685")
}

processResources {
	def toReplace = [
			"version": project.version,
	]

	println("[Process Resources] Replacing properties in resources: " + toReplace)

	inputs.properties toReplace
	filesMatching("META-INF/neoforge.mods.toml") {
		expand toReplace
	}
}

jar {
	manifest {
		attributes([
				"Specification-Title"     : project.mod_id,
				"Specification-Vendor"    : project.mod_author,
				"Specification-Version"   : "1",
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : version,
				"Implementation-Vendor"   : project.mod_author,
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

unifiedPublishing {
	project {
		releaseType = "${ENV.RELEASE_TYPE ?: 'release'}"
		gameVersions = [
				rootProject.minecraft_version
		]
		gameLoaders = ["neoforge"]
		displayName = "$mod_name NeoForge $project.version"
		mainPublication jar

		if (ENV.CURSEFORGE_KEY) {
			curseforge {
				token = ENV.CURSEFORGE_KEY
				id = project.curseforge_id
			}
		}

		if (ENV.MODRINTH_TOKEN) {
			modrinth {
				token = ENV.MODRINTH_TOKEN
				id = project.modrinth_id
				version = "$project.version+$project.name"
			}
		}
	}
}

publishing {
	publications {
		mavenNeoForge(MavenPublication) {
			artifactId = archives_base_name
			from components.java
		}
	}

	repositories {
		if (ENV.MAVEN_TOKEN) {
			maven {
				url "https://maven.latvian.dev/releases"
				credentials {
					username = "lat"
					password = "${ENV.MAVEN_TOKEN}"
				}
			}
		}

		if (ENV.SAPS_TOKEN) {
			maven {
				url "https://maven.saps.dev/releases"
				credentials {
					username = "latvian"
					password = "${ENV.SAPS_TOKEN}"
				}
			}
		}
	}
}

afterEvaluate {
	tasks.withType(JavaCompile) {
		options.compilerArgs << "-Xmaxerrs" << "1000"
	}
}